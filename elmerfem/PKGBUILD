# Contributor: Anton Kudelin <kudelin at protonmail dot com>
# Contributor:  saxonbeta <saxonbeta [[at]] gmail _dot com
# Contributor: Randy Heydon <randy dot heydon at clockworklab dot net>

pkgname=elmerfem
pkgver=9.0
pkgrel=4.1
pkgdesc="A finite element software for multiphysical problems"
arch=('x86_64')
url="http://www.elmerfem.org"
license=('GPL')
depends=('arpack' 'qt5-script' 'netcdf-fortran-openmpi' 'mmg' 'qwt' 'hypre' 'oce' 'libopenblas' 'mumps-par' 'paraview-opt')
optdepends=('metis: Split grids to perform parallel runs')
makedepends=('gcc-fortran' 'cmake')
conflicts=('elmerfem-git')
options=(!emptydirs)
source=("https://github.com/ElmerCSC/$pkgname/archive/release-$pkgver.tar.gz"
        "$pkgname.desktop"
        "ElmerIce.patch"
        "progateIndex.patch")
sha256sums=('08c5bf261e87ff37456c1aa0372db3c83efabe4473ea3ea0b8ec66f5944d1aa0'
            'f4b39389e5f258c7860b8d7a6b171fb54bf849dc772f640ac5e7a12c7a384aca'
            '90287c988ac4f5beedf5221e81f624799ec3253c63a30695e1873044ac5a6515'
            '4a4e69ee31f404ac856a409fe66cfde6431d0e0168dac7595a26818d0934cf76')

prepare() {
  cd "$srcdir/$pkgname-release-$pkgver"
  [ -d "../build" ] && rm ../build -R
  mkdir ../build
  patch -p0 < "$srcdir/ElmerIce.patch"
  patch -p1 < "$srcdir/progateIndex.patch"
  sed -i '/#include <QPainter>/a #include <QPainterPath>' ElmerGUI/Application/twod/renderarea.cpp
  
}

build() {
  cd "$srcdir/build"
  cmake ../$pkgname-release-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_Fortran_FLAGS="$FCFLAGS -fallow-argument-mismatch -fallow-invalid-boz -Wdeprecated-declarations" \
    -DMPI_Fortran_COMPILE_FLAGS="$FCFLAGS -fallow-argument-mismatch -fallow-invalid-boz -Wdeprecated-declarations" \
    -DELMER_INSTALL_LIB_DIR=/usr/lib \
    -DWITH_ELMERGUI=ON \
    -DWITH_LUA=ON \
    -DWITH_MATC=ON \
    -DWITH_MPI=ON \
    -DWITH_OpenMP=ON \
    -DWITH_QT5=ON \
    -DWITH_ScatteredDataInterpolator=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DWITH_Hypre=ON \
    -DHYPRE_INCLUDE_DIR=/usr/include/hypre \
    -DWITH_QWT=ON \
    -DWITH_OCC=ON \
    -DWITH_Mumps=ON \
    -DWITH_VTK=ON \
    -DWITH_PARAVIEW=ON 
  make all
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  cd "$pkgdir/usr"
  
  # Remove unecessary libraries
  rm -rf -- lib/{*.a,*arpack.so,ElmerGUI}

  #Create directories
  install -dv share/applications
  install -dv share/pixmaps
  install -dv share/licenses/$pkgname
  
  #Uncomment to install sources, if debugging is needed. Also, add the options !strip debug.
  #install -dv src/debug/$pkgname-release-$pkgver
  #cp -r "$srcdir"/$pkgname-release-$pkgver/* src/debug/$pkgname-release-$pkgver/

  #Icon and desktop files
  install -D -m644 "$srcdir/$pkgname-release-$pkgver/ElmerGUI/Application/images/logo.png" share/pixmaps/$pkgname.png
  install -D -m644 "$srcdir/$pkgname.desktop" share/applications

  #Install additional edf files (GUI solvers)
  cp share/ElmerGUI/edf-extra/* share/ElmerGUI/edf
  
  #Clean up and move stuff in place
  mv share/ElmerGUI/license_texts/GPL_EXCEPTION share/licenses/$pkgname
  #mv share/elmersolver/lib/* lib/
  rm share/ElmerGUI/license_texts/*
}
