pkgname=salome-bin
pkgver=9.7.0
pkgrel=3
pkgdesc='Generic platform for Pre and Post-Processing for numerical simulation. MPI Version'
arch=('x86_64')
url='https://www.salome-platform.org'
license=('LGPL')
depends=('coreutils' 'inetutils' 'net-tools' 'blas')
provides=('salome')
options=(!strip)
#To use use the non-mpi version, remove "mpi" from the source URL and update the SHA256 sum
source=(
    "${pkgname}-${pkgver}.tar.gz::${url}/downloads/current-version/DownloadDistr?platform=SP.UNILINmpi&version=${pkgver}"
    "nameserver.patch"
    "${pkgname}.desktop"
)
sha256sums=('513d436eec3e2fd9d03a08386599bb84c145608d669c71d429b97005d4290fb4'
            '467966b59321f37a5eec393fdf2525d696ba82e0e4ea144ee2c2d52d0344fc3e'
            'e2c9c057fbc659775e9c43807a9bb1ed032b451d82fbce301c934b8dd5178a8e')
 
prepare() {
    cd "${srcdir}"
    patch -p0 < "${srcdir}/nameserver.patch"
}

package(){
    cd "${pkgdir}"
    
    #Create directories
    install -dv usr/share/{applications,pixmaps}
    install -dv usr/bin
    install -dv opt/$pkgname
    
    #Create executable
    cat > "${pkgdir}/usr/bin/salome" <<-EOF
    #!/usr/bin/env bash
    exec /opt/salome-bin/salome
	EOF
    chmod +x usr/bin/salome
    
    #Icon and desktop files
    #To use use the non-mpi version, adjust the following line
    install -D -m644 "${srcdir}/SALOME-${pkgver}-MPI/BINARIES-CO7/SALOME/share/salome/resources/salome/app_icon.png" usr/share/pixmaps/$pkgname.png
    install -D -m644 "${srcdir}/${pkgname}.desktop" usr/share/applications

	#Install application
	#To use use the non-mpi version, adjust the following line
	mv "${srcdir}/SALOME-${pkgver}-MPI"/{BINARIES-CO7,PROJECT,SOURCES,salome,binsalome,mesa_salome,env_launch.sh} "opt/${pkgname}"
}
