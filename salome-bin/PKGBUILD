pkgname=salome-bin
pkgver=9.7.0
pkgrel=1
pkgdesc='Generic platform for Pre and Post-Processing for numerical simulation'
arch=('x86_64')
url='https://www.salome-platform.org'
license=('LGPL')
depends=(
    'coreutils'
    'inetutils'
    'net-tools'
    'openblas'
)
provides=('salome')
options=(!strip)
# Download the Universal Linux binaries (normal or mpi-based versions) from 
#https://www.salome-platform.org/downloads/current-version. 
#Then, rename the file to salome.tar.gz
source=(
    "salome.tar.gz"
    "nameserver.patch"
    "${pkgname}.desktop"
)
sha256sums=('SKIP'
            '467966b59321f37a5eec393fdf2525d696ba82e0e4ea144ee2c2d52d0344fc3e'
            '5da4daed1f88f02f3868107efa0c38cdacf30ca38eb4ff37d83c382988f54677')
 
prepare() {
    cd "${srcdir}"
    patch -p0 < "${srcdir}/nameserver.patch"
}

package(){
    cd "${pkgdir}"
    
    #Create directories
    install -dv usr/share/applications
    install -dv usr/share/pixmaps
    install -dv usr/bin
    install -dv opt/salome-bin
    
    #Create executable
    cat > "${pkgdir}/usr/bin/salome" <<-EOF
    #!/usr/bin/env bash
    exec /opt/salome-bin/salome
	EOF
    chmod +x usr/bin/salome
    
    #Icon and desktop files
    #If the MPI version is not used, adjust the following line
    install -D -m644 "${srcdir}/SALOME-${pkgver}-MPI/BINARIES-CO7/SALOME/share/salome/resources/salome/app_icon.png" usr/share/pixmaps/$pkgname.png
    install -D -m644 "${srcdir}/${pkgname}.desktop" usr/share/applications

	#Install application
	#If the MPI version is not used, adjust the following line
	mv "${srcdir}/SALOME-${pkgver}-MPI"/{BINARIES-CO7,PROJECT,SOURCES,salome,binsalome,mesa_salome,env_launch.sh} "${pkgdir}/opt/salome-bin"
}
