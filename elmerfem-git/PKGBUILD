# Maintainer: E. Albiter
# Contributor: Anton Kudelin <kudelin at protonmail dot com>
# Contributor:  saxonbeta <saxonbeta [[at]] gmail _dot com
# Contributor: Randy Heydon <randy dot heydon at clockworklab dot net>

pkgname=elmerfem-git
_pkgname=elmerfem
pkgver=9.0.r562.gdd155814
pkgrel=1
pkgdesc="A finite element software for multiphysical problems"
arch=('x86_64')
url="http://www.elmerfem.org"
license=('GPL')
depends=('arpack' 'qt5-script' 'netcdf-fortran-openmpi' 'mmg' 'qwt' 'oce' 'blas' 'mumps-par' 'paraview-opt')
optdepends=('metis: Split grids to perform parallel runs')
makedepends=('gcc-fortran' 'cmake' 'git')
conflicts=("${_pkgname}")
options=(!emptydirs)
source=("git+https://github.com/ElmerCSC/elmerfem.git"
        "$_pkgname.desktop"
        "progateIndex.patch")
sha256sums=('SKIP'
            'f4b39389e5f258c7860b8d7a6b171fb54bf849dc772f640ac5e7a12c7a384aca'
            '4a4e69ee31f404ac856a409fe66cfde6431d0e0168dac7595a26818d0934cf76')

            
pkgver() {
  git -C "${srcdir}/${_pkgname}" describe --long --tag| sed -r 's/^release-//;s/([^-]*-g)/r\1/;s/-/./g'
}


prepare() {
  cd "$srcdir/$_pkgname"
  [ -d "../build" ] && rm ../build -R
  mkdir ../build
  patch -p1 < "$srcdir/progateIndex.patch"
  sed -i 's/sprintf(str_pstr, fmt);/sprintf(str_pstr, "%s", fmt);/g' matc/src/str.c
}

build() {
  cd "$srcdir/build"
  cmake ../$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_Fortran_FLAGS="$FCFLAGS -fallow-argument-mismatch -fallow-invalid-boz" \
    -DMPI_Fortran_COMPILE_FLAGS="$FCFLAGS -fallow-argument-mismatch -fallow-invalid-boz" \
    -DELMER_INSTALL_LIB_DIR=/usr/lib \
    -DWITH_ELMERGUI=ON \
    -DWITH_ElmerIce=OFF \
    -DWITH_LUA=ON \
    -DWITH_MATC=OFF \
    -DWITH_MPI=ON \
    -DWITH_OpenMP=ON \
    -DWITH_QT5=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DWITH_Hypre=OFF \
    -DWITH_QWT=ON \
    -DWITH_OCC=ON \
    -DWITH_Mumps=ON \
    -DWITH_VTK=ON \
    -DWITH_PARAVIEW=ON 
  make all
    
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
  cd "$pkgdir/usr"
  
  # Remove unecessary libraries
  rm -rf -- lib/{*.a,*arpack.so,ElmerGUI}

  #Create directories
  install -dv share/{applications,pixmaps,licenses/$pkgname}
  
  #Uncomment to install sources, if debugging is needed. Also, add the options !strip debug.
  #install -dv src/debug/$pkgname-release-$pkgver
  #cp -r "$srcdir"/$pkgname-release-$pkgver/* src/debug/$pkgname-release-$pkgver/

  #Icon and desktop files
  install -D -m644 "$srcdir/$_pkgname/ElmerGUI/Application/images/logo.png" share/pixmaps/$pkgname.png
  install -D -m644 "$srcdir/$_pkgname.desktop" share/applications

  #Install additional edf files (GUI solvers)
  cp share/ElmerGUI/edf-extra/* share/ElmerGUI/edf
  
  #Clean up and move stuff in place
  mv share/ElmerGUI/license_texts/GPL_EXCEPTION share/licenses/$pkgname
  rm share/ElmerGUI/license_texts/*
}
